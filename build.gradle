plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.cyclonedx.bom' version '1.10.0'
}

group = 'com.sprint.mission'
version = '0.0.1-SNAPSHOT'
description = 'SB8-HRBANK-team-Querity'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    runtimeOnly 'org.postgresql:postgresql'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // springdoc-openapi
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14'

    // mapstruct, lombok보다 뒤에 있어야 겹치는 getter,setter 등의 어노테이션이
    // 문제 없이 동작한다고 한다.
    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

    // 1. Querydsl 라이브러리
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'

    // 2. Querydsl 쿼리 타입(QClass) 생성을 위한 이노테이션 프로세서
    annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // Apache poi API 의존성 추가
    implementation("org.apache.poi:poi-ooxml:5.5.1")
    implementation("org.apache.poi:poi:5.5.1")
}

// QClass 생성 위치 설정
def querydslDir = "src/main/generated"

sourceSets {
    main.java.srcDirs += [ querydslDir ]
}

tasks.withType(JavaCompile).configureEach {
    options.getGeneratedSourceOutputDirectory().set(file(querydslDir))
}

clean {
    delete file(querydslDir)
}

tasks.named('test') {
    useJUnitPlatform()
}

// 배포용
bootJar {
    enabled = true
}
